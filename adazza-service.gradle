/*
Apply this plugin to the root build file to configure your finagle service.
*/
buildscript {
  repositories {
    jcenter()
  }
  dependencies {
    classpath 'com.netflix.nebula:gradle-ospackage-plugin:4.3.0'
  }
}
// If your root project is foo-service the name is foo.
// The submodules for your service will then be foo-api, 
// foo-server, foo-client.
def serviceName = rootProject.name.replace('-service', '')

allprojects {
  apply from: 'https://raw.githubusercontent.com/adazza/adazza-gradle/master/adazza-scala.gradle'
}

apply from: 'https://raw.githubusercontent.com/adazza/adazza-gradle/master/adazza-core.gradle'

configure(subprojects.findAll { it.name.endsWith('-api') }) {
}

configure(subprojects.findAll { it.name.endsWith('-client') }) {
  dependencies {
    compile project(":${serviceName}-api")
  }
}

configure(subprojects.findAll { it.name.endsWith('-server') }) {
  def adazzaServiceUser = 'adazzaservice'
  def package_arch = 'amd64'
  apply plugin: 'application'
  apply plugin: com.netflix.gradle.plugins.application.OspackageApplicationDaemonPlugin

  ospackage {
    os = 'LINUX' // only applied to RPM
    //Versions don't allow - or ~ so we replace with .
    version = project.version.toString().replace('~', '.').replace('-', '.')
    arch = package_arch
  }

  applicationdaemon {
    user = adazzaServiceUser
    command = "/opt/adazza/bin/start-adazza-service $project.name $project.version"
    autoStart = false
  }

  dependencies {
    compile project(":${serviceName}-api")
  }

  repositories {
    jcenter()
  }

  run {
    if(System.getProperty("exec.args") != null) {
      args System.getProperty("exec.args").split()
    }
  }

  task publishDeb(type: Exec) {
    workingDir "$project.projectDir"
    commandLine 'deb-s3', 'upload', '--lock', '--visibility=private',
                '--bucket=adazza-deb-repo', '--prefix=repo', '--origin=jenkins',
                '--s3-region=us-west-1', '--sign="Adazza Deb Repo"',
                '-c', 'stable', '-m', 'services',
                "$project.buildDir/distributions/${project.name}_${version}_${package_arch}.deb"
  }

  publishDeb.dependsOn buildDeb
}
